buildscript {
	repositories {
		mavenCentral()
	}
	
	dependencies {
		classpath "com.monochromeroad.gradle-plugins:gradle-aws-s3-sync:0.10"
	}
}

//plugins {
//	//// tool to see tasks dependencies, use: gradlew <mytask> taskTree
//    id 'com.dorongold.task-tree' version '1.5'
//}

import com.monochromeroad.gradle.plugin.aws.s3.S3Sync
import org.apache.tools.ant.filters.ReplaceTokens
import groovy.xml.XmlUtil

apply plugin: 'eclipse'
apply plugin: 'maven-publish'

ext.convertigoVersion = '8.2.1'
ext.convertigoTag = 'beta'
ext.convertigoCodename = 'lead'
ext.convertigoCode = 'Pb'
ext.convertigoRevision = hasProperty('convertigoRevision') ? convertigoRevision : ''
ext.jxBrowserGroup = 'com.teamdev.jxbrowser'
//ext.jxBrowserGroup = 'com.convertigo.lib'
ext.jxBrowserVersion = '7.30.3'
ext.swaggerUiVersion = '3.19.3'
ext.tomcatVersion = '9.0.73'
ext.eclipseVersion = '2022-12'
ext.eclipseBase = '4.26'
ext.sapjco3Version = 'com.convertigo.lib:sapjco3:2019.09.20'

ext.convertigoMajor = convertigoVersion.replaceFirst('(\\d+\\.\\d+)\\..*', '$1')
ext.convertigoEngineAPI = "${convertigoVersion}-SNAPSHOT"

ext.convertigoVersionFull = "${convertigoVersion}${convertigoTag.isEmpty() ? '' : '-' + convertigoTag}"
ext.currentYear = '' + Calendar.getInstance().get(Calendar.YEAR)

apply from: 'headers.gradle'
apply from: 'changelog.gradle'
apply from: 'installers/windows.gradle'

task updateVersion(dependsOn: [':engine:updateProductVersion', 'writeManifest', 'generateDockerfile']) {
	group 'convertigo'
	description 'Run this task after a Convertigo version change.'
}

configurations.all {
	// Check for updates every build
	resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

eclipse {
	project {
		comment = 'Main project to develop the Convertigo platform'
	}
}

allprojects {
	version convertigoVersionFull
	
	repositories {
		mavenLocal()
		maven {
			url "https://m2.convertigo.com"
		}
		maven {
			url "https://europe-maven.pkg.dev/jxbrowser/releases"
		}
		mavenCentral()
	}
	
	if (project.name.startsWith('eclipse')) {
		apply plugin: 'eclipse'
	}
	
	if (project.name.startsWith('eclipse-plugin')) {
		apply plugin: 'java'
		
		sourceSets {
			main {
				java {
					exclude '.placeholder'
					srcDirs = ['src']
				}
				resources {
					exclude '.placeholder'
					srcDirs = ['src']
				}
			}
		}
		
		eclipse {
			project {
				natures 'org.eclipse.jdt.core.javanature',
						'org.eclipse.pde.PluginNature',
						'org.eclipse.buildship.core.gradleprojectnature'
				buildCommand 'org.eclipse.pde.ManifestBuilder'
				buildCommand 'org.eclipse.buildship.core.gradleprojectbuilder'
			}
			
			classpath.file.withXml {
				def node = it.asNode()
				if (node.classpathentry.find { it.@path == 'org.eclipse.pde.core.requiredPlugins' } == null) {
					node.appendNode('classpathentry', [kind: 'con', path: 'org.eclipse.pde.core.requiredPlugins'])
				}
				def projectPath = projectDir.path.replace('\\', '/') + '/'
				node.classpathentry.findAll { it.@kind == 'lib' }.each {
					println 'classpath : ' + it.@path.replace(projectPath, '')
					it.@path = it.@path.replace(projectPath, '')
				}
			}
		}
		
		configurations {
			toSync
		}
		
		task syncLib(type: Sync) {
			group 'convertigo'
			
			into 'lib'
			from configurations.toSync
			
			rename 'sapjco3.*jar', 'sapjco3.jar'
		}
	}
	
	plugins.withId('java') {
		compileJava.options.encoding = 'UTF-8'
		sourceCompatibility = '11'
		targetCompatibility = '11'
		
		task cleanBin(type: Delete) {
			group 'convertigo'
			
			def f = project.file('bin')
			if (f.exists()) {
				f.eachFile {
					if (it.getName() != 'main') {
						println it
						delete it
					}
				}
			}
		}
	}
	
	afterEvaluate {
		if (plugins.hasPlugin('maven-publish')) {
			publishing {
				repositories {
					maven {
						url project.properties['c8o.m2.url']
						credentials(AwsCredentials) {
							accessKey project.properties['c8o.m2.ak']
							secretKey project.properties['c8o.m2.sk']
						}
					}
				}
			}
		}
	}
}

publishing {
	publications {
		engineApi (MavenPublication) {
			groupId 'com.convertigo.lib'
			artifactId 'convertigo-engine-api'
			version convertigoEngineAPI
			afterEvaluate {
				artifact project(':engine').engineApiJar
				pom.withXml {
					def dependencies = asNode().appendNode('dependencies')
					[
						'org.apache.logging.log4j:log4j-core:2.20.0',
						'org.apache.logging.log4j:log4j-1.2-api:2.20.0',
						'com.convertigo.twinsoft:api:2017.12.29',
						'com.convertigo.twinsoft:tas:2019.10.16',
						'com.convertigo.twinsoft:util:2017.12.29',
						'com.fasterxml.jackson.core:jackson-core:2.15.0',
						'commons-io:commons-io:2.11.0',
						'javax.mail:mail:1.4.7',
						'javax.servlet:servlet-api:2.5',
						'org.apache.commons:commons-lang3:3.7',
						'org.apache.httpcomponents:httpclient:4.5.14',
						'org.codehaus.jettison:jettison:1.5.4'
					].each {
						def values = it.split(':')
						def dependency = dependencies.appendNode('dependency')
						dependency.appendNode('groupId', values[0])
						dependency.appendNode('artifactId', values[1])
						dependency.appendNode('version', values[2])
					}
				}
			}
		}
		
		jxBrowser (MavenPublication) {
			groupId 'com.convertigo.lib'
			artifactId 'jxbrowser'
			version "${jxBrowserVersion}"
			artifact "resources/jxbrowser-${jxBrowserVersion}/jxbrowser-${jxBrowserVersion}.jar"
		}
		
		jxBrowserLinux64 (MavenPublication) {
			groupId 'com.convertigo.lib'
			artifactId 'jxbrowser-linux64'
			version "${jxBrowserVersion}"
			artifact "resources/jxbrowser-${jxBrowserVersion}/jxbrowser-linux64-${jxBrowserVersion}.jar"
		}
		
		jxBrowserMac (MavenPublication) {
			groupId 'com.convertigo.lib'
			artifactId 'jxbrowser-mac'
			version "${jxBrowserVersion}"
			artifact "resources/jxbrowser-${jxBrowserVersion}/jxbrowser-mac-${jxBrowserVersion}.jar"
		}
		
		jxBrowserWin64 (MavenPublication) {
			groupId 'com.convertigo.lib'
			artifactId 'jxbrowser-win64'
			version "${jxBrowserVersion}"
			artifact "resources/jxbrowser-${jxBrowserVersion}/jxbrowser-win64-${jxBrowserVersion}.jar"
		}
		
		jxBrowserSwt (MavenPublication) {
			groupId 'com.convertigo.lib'
			artifactId 'jxbrowser-swt'
			version "${jxBrowserVersion}"
			artifact "resources/jxbrowser-${jxBrowserVersion}/jxbrowser-swt-${jxBrowserVersion}.jar"
		}
		
		ojdbc (MavenPublication) {
			groupId 'com.convertigo.lib'
			artifactId 'ojdbc7'
			version '12.1.0.2'
			artifact 'resources/ojdbc7.jar'
		}
		
		xmlbeans (MavenPublication) {
			groupId 'com.convertigo.lib'
			artifactId 'xmlbeans'
			version '2.6.0'
			artifact 'resources/xmlbeans-2.6.0.jar'
		}
	}
}

afterEvaluate {
	ext.sharedManifest = project(':engine').manifest {
		attributes(
			'Bundle-ManifestVersion': '2',
			'Bundle-Vendor': 'Convertigo',
			'Bundle-Version': "${convertigoVersion}.qualifier",
			'Bundle-RequiredExecutionEnvironment': 'JavaSE-11',
		)
	}
}

task writeManifest(dependsOn:  ['updateEclipseVersion', 'makeSplash']) {
	group 'convertigo'
	
	def wm = getTasksByName 'writeManifest', true
	wm.remove tasks['writeManifest']
	wm.each { it.dependsOn ':updateEclipseVersion' }
	dependsOn wm
}

task buildStudio(type: Exec, dependsOn: ['writeManifest', 'downloadSystemLibraries']) {
	group 'convertigo'
	
	onlyIf { !file('eclipse-repository/target/products').exists() }
	
	if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
		commandLine 'cmd', '/c', 'mvn'
	} else {
		commandLine 'mvn'
	}
	
	args 'install'
}

task updateEclipseProduct(type: Copy, dependsOn: [':checkLicenseDate', 'updateEclipseCategory', ':eclipse-plugin-product:writePluginXml', ':eclipse-plugin-product:writeAboutIni']) {
	group 'convertigo'
	
	def aboutTextFile = file('eclipse-plugin-product/aboutText.txt')
	def licenseTextFile = file('license.txt')
	
	inputs.properties(
		'currentYear': currentYear,
		'convertigoVersionFull': convertigoVersionFull
	)
	inputs.files aboutTextFile, licenseTextFile
	
	def aboutText = ''
	def licenseText = ''
	
	doFirst {
		def xml = new XmlSlurper().parseText('<elt/>')
		xml.replaceBody aboutTextFile.text
			.replace('@CEMS_VERSION@', convertigoVersionFull)
			.replace('@CURRENT_YEAR@', currentYear)
		aboutText = XmlUtil.serialize(xml).replaceFirst('[\\d\\D]*<elt>([\\d\\D]*)</elt>[\\d\\D]*','$1')
		
		xml.replaceBody licenseTextFile.text
		licenseText = XmlUtil.serialize(xml).replaceFirst('[\\d\\D]*<elt>([\\d\\D]*)</elt>[\\d\\D]*','$1')
	}
	
	from 'eclipse-repository/Convertigo.product.tpl'
	filter {
		it.replace('@aboutText@', aboutText)
			.replace('@licenseText@', licenseText)
			.replace('@convertigoVersion@', convertigoVersion)
			.replace('@convertigoMajor@', convertigoMajor)
			.replace('@convertigoMode@', convertigoTag.isEmpty() ? 'release' : 'beta')
			.replace('@eclipseVersion@', eclipseVersion)
	}
	into 'eclipse-repository'
	rename '.*', 'Convertigo.product'
}

task updateEclipseCategory(type: Copy) {
	group 'convertigo'
	
	inputs.properties(
		'convertigoVersion': convertigoVersion
	)
	
	from 'eclipse-repository/category.xml.tpl'
	filter { it.replace('@convertigoVersion@', convertigoVersion) }
	into 'eclipse-repository'
	rename '.*', 'category.xml'
}

task updateEclipseVersion(dependsOn: [':updateEclipseProduct', ':eclipse-plugin-product:writeAboutIni', ':eclipse-plugin-product:writePluginXml', ':eclipse-feature:updateFeature']) {
	group 'convertigo'
	doFirst {
		fileTree(dir: '.', include: '*/pom.xml').files.plus(file('pom.xml')).each {
			it.text = it.text.replaceAll('(<!-- SET-VERSION -->\\s*<version>).*(</version>)', "\$1${convertigoVersion}-SNAPSHOT\$2")
		}
		def base = file('eclipse-base/base.target')
		base.text = base.text.replaceFirst('/releases/.+?/', "/releases/${eclipseVersion}/").replaceFirst('/updates/.+?/', "/updates/${eclipseBase}/")
	}
}

task buildStudioClean(type: Delete) {
	group 'convertigo'
	
	delete 'eclipse-repository/target/products'
}

task generateEclipseConfiguration(dependsOn: [getTasksByName('cleanEclipse', true), getTasksByName('eclipse', true), getTasksByName('cleanBin', true)]) {
	group 'convertigo'
}

task generateEclipseConfigurationWithManifest(dependsOn: ['writeManifest', 'generateEclipseConfiguration']) {
	group 'convertigo'
}

task makeSplash(dependsOn: 'convertigo-splash:run') {
	group 'convertigo'
}

task ciFirstStep(dependsOn: [':engine:jar', ':gradle-plugin:publish']) {
	group 'convertigo'
}

task generateDockerfile {
	group 'convertigo'
	
	doLast {
		def base = file('docker/Dockerfile.tpl').text
		base = base.replace('%VERSION%', convertigoVersion)
		
		file('docker/default/Dockerfile').text = base.replace('%BEGIN%', '')
		file('docker/default/docker-entrypoint.sh').text = file('docker/docker-entrypoint.sh').text.replace('%ON_LAUNCH%', '')
		file('docker/default/root-index.html').text = file('docker/root-index.html').text
		
		file('docker/aks/Dockerfile').text = base.replace('%BEGIN%', file('docker/aks-Dockerfile.tpl').text)
		file('docker/aks/docker-entrypoint.sh').text = file('docker/docker-entrypoint.sh').text.replace('%ON_LAUNCH%', file('docker/aks-entrypoint.tpl').text)
		file('docker/aks/root-index.html').text = file('docker/root-index.html').text
	}
}

task downloadSystemLibFlowViewer(type: Sync) {
	group 'convertigo'
	
	def car = file 'lib_FlowViewer.car'
	ext.dir = file 'eclipse-plugin-studio/tomcat/webapps/convertigo/system/projects/'
	
	from 'eclipse-plugin-studio/src'
	into ext.dir
		
	doFirst {
		new URL('https://github.com/convertigo/c8oprj-lib-flowviewer/releases/download/ci/lib_FlowViewer.car')
			.withInputStream{ i -> car.withOutputStream{ it << i }}
		from zipTree(car)
		exclude 'com/**'
	}
	
	doLast {
		car.delete()
	}
}

task downloadSystemLibraries(/*dependsOn: downloadSystemLibFlowViewer*/) {
	group 'convertigo'
}

task checkLicenseDate {
	group 'convertigo'
	
	def licenseTextFile = file('license.txt')
	
	inputs.properties(
		'currentYear': currentYear
	)
	inputs.files licenseTextFile
	outputs.files licenseTextFile
	
	doFirst {
		licenseTextFile.text = licenseTextFile.text.replaceAll('-20\\d\\d-', "-${currentYear}-")
	}
}
