import groovy.xml.XmlUtil

task writeManifest(dependsOn: syncLib) {
	group 'convertigo'
	
	doLast {
		def manifest = file 'META-INF/MANIFEST.MF'
		project.java.manifest {
			from sharedManifest
			attributes(
				'Automatic-Module-Name': 'com.twinsoft.convertigo.studio.product',
				'Bundle-Name': 'C-EMS studio product',
				'Bundle-SymbolicName': 'com.twinsoft.convertigo.studio.product;singleton:=true',
				'Bundle-Activator': 'com.twinsoft.convertigo.studio.product.Activator',
				'Bundle-ActivationPolicy': 'lazy',
				'Bundle-ClassPath': 'lib/convertigo-product.jar',
				'Export-Package': 'com.twinsoft.convertigo.studio.product;uses:="org.osgi.framework,org.eclipse.core.runtime",'
					+'com.twinsoft.convertigo.studio.product.splashHandlers;uses:="org.eclipse.ui.splash,org.eclipse.swt.widgets"',
				'Require-Bundle': ['org.eclipse.core.runtime',
									'org.eclipse.ui',
									'org.eclipse.ui.intro.universal',
									'org.eclipse.ui.intro',
									'org.eclipse.ui.themes',
									'org.eclipse.swt'].join(',')
			)
		}.writeTo(manifest)
		manifest.text = manifest.text.replaceAll("Import-Package: .*\\s+", "")
	}
}

task writePluginXml(type: Copy) {
	group 'convertigo'
	
	def aboutTextFile = file('aboutText.txt')
	
	inputs.properties(
		'currentYear': currentYear,
		'convertigoVersionFull': convertigoVersionFull
	)
	inputs.files aboutTextFile
	
	def aboutText = ''
	
	doFirst {
		def xml = new XmlSlurper().parseText('<elt/>')
		xml.@value = aboutTextFile.text
			.replace('@CEMS_VERSION@', convertigoVersionFull)
			.replace('@CURRENT_YEAR@', currentYear)
		aboutText = XmlUtil.serialize(xml).replaceFirst('[\\d\\D]*?value="(.*)"/>[\\d\\D]*','$1')
	}
	
	from 'plugin.xml.tpl'
	filter { it.replace('@aboutText@', aboutText) }
	into '.'
	rename '.*', 'plugin.xml'
}

task writeAboutIni(type: Copy, dependsOn: writePluginXml) {
	group 'convertigo'
	
	inputs.properties(
		'currentYear': currentYear
	)
	
	from 'about.ini.tpl'
	filter { it.replace('((CURRENT_YEAR))', currentYear) }
	into '.'
	rename '.*', 'about.ini'
}