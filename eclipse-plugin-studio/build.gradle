sourceSets {
	main {
		java {
			exclude 'com/twinsoft/convertigo/engine/plugins/EnvoiSMSBiller.java'
			exclude 'com/twinsoft/convertigo/engine/plugins/InfogreffeBiller.java'
			exclude 'com/twinsoft/convertigo/engine/plugins/InseeBiller.java'
			exclude 'com/twinsoft/convertigo/engine/plugins/LCABiller.java'
			srcDir 'patch'
		}
		resources {
			exclude 'com/twinsoft/convertigo/engine/plugins/EnvoiSMSBiller.java'
			exclude 'com/twinsoft/convertigo/engine/plugins/InfogreffeBiller.java'
			exclude 'com/twinsoft/convertigo/engine/plugins/InseeBiller.java'
			exclude 'com/twinsoft/convertigo/engine/plugins/LCABiller.java'
			srcDir 'patch'
		}
	}
}

eclipse {
	project {
		linkedResource name: 'patch/com', type: '2', locationUri: 'PARENT-1-PROJECT_LOC/patches/src/com'
		linkedResource name: 'patch/org', type: '2', locationUri: 'PARENT-1-PROJECT_LOC/patches/src/org'
		linkedResource name: 'src/com/twinsoft/convertigo/engine', type: '2', locationUri: 'PARENT-1-PROJECT_LOC/engine/src/com/twinsoft/convertigo/engine'
		linkedResource name: 'src/com/twinsoft/convertigo/beans', type: '2', locationUri: 'PARENT-1-PROJECT_LOC/engine/src/com/twinsoft/convertigo/beans'
		linkedResource name: 'src/com/twinsoft/convertigo/engine/parsers', type: '2', locationUri: 'PARENT-2-PROJECT_LOC/convertigo-provider-xul/src/com/twinsoft/convertigo/engine/parsers'
		linkedResource name: 'src/com/twinsoft/convertigo/engine/plugins', type: '2', locationUri: 'PARENT-2-PROJECT_LOC/convertigo-provider-plugins/src/com/twinsoft/convertigo/engine/plugins'
		linkedResource name: 'src/com/twinsoft/convertigo/engine/providers/couchdb', type: '2', locationUri: 'PARENT-2-PROJECT_LOC/convertigo-provider-couchdb/src/com/twinsoft/convertigo/engine/providers/couchdb'
		linkedResource name: 'src/com/twinsoft/convertigo/engine/providers/sapjco', type: '2', locationUri: 'PARENT-2-PROJECT_LOC/convertigo-provider-sap/src/com/twinsoft/convertigo/engine/providers/sapjco'
	}
	
	classpath.file.withXml {
		def node = it.asNode()
		def rp = null
		if ((rp = node.classpathentry.find { it.@path == 'org.eclipse.pde.core.requiredPlugins' }) == null) {
			rp = node.appendNode('classpathentry', [kind: 'con', path: 'org.eclipse.pde.core.requiredPlugins'])
		}
		def accessrules = rp.appendNode('accessrules')
		accessrules.appendNode('accessrule', [kind: 'accessible', pattern: 'org/eclipse/egit/ui/**'])
		accessrules.appendNode('accessrule', [kind: 'accessible', pattern: 'org/eclipse/e4/ui/**'])
		
		def depJar
		if ((depJar = node.classpathentry.find { it.@path.contains('libs/dependencies') }) != null) {
			depJar.@sourcepath = depJar.@sourcepath = depJar.@path.replace('libs/dependencies', 'libs/dependencies-sources')
		}
	}
}

tasks['eclipse'].dependsOn ':engine:dependenciesSourceJar'

dependencies {
	runtimeOnly project(':engine')
	runtimeOnly 'com.convertigo.lib:jxbrowser-license:2019.04.02'
	runtimeOnly "${jxBrowserGroup}:jxbrowser:${jxBrowserVersion}"
	runtimeOnly "${jxBrowserGroup}:jxbrowser-swt:${jxBrowserVersion}"
	runtimeOnly 'org.apache.tomcat.embed:tomcat-embed-core:' + tomcatVersion
	//runtimeOnly 'org.apache.tomcat.embed:tomcat-embed-logging-log4j:' + tomcatVersion
	runtimeOnly 'org.apache.tomcat:tomcat-dbcp:' + tomcatVersion
	runtimeOnly 'org.apache.axis2:axis2-adb:1.6.2@jar'
	runtimeOnly project.property('sapjco3Version')
}

task writeManifest(dependsOn: ['copyLicense', 'syncLib', ':engine:unzipSwaggerUI']) {
	group 'convertigo'
	
	doLast {
		def manifest = file 'META-INF/MANIFEST.MF'
		project.manifest {
			from sharedManifest
			attributes(
				'Automatic-Module-Name': 'com.twinsoft.convertigo.studio',
				'Bundle-Name': 'C-EMS studio plugin',
				'Bundle-SymbolicName': 'com.twinsoft.convertigo.studio; singleton:=true',
				'Eclipse-BundleShape': 'dir',
				'Bundle-Activator': 'com.twinsoft.convertigo.eclipse.ConvertigoPlugin',
				'Bundle-ActivationPolicy': 'lazy',
				'Bundle-ClassPath': 'bin/,icons/,tomcat/webapps/convertigo/WEB-INF/classes,lib/convertigo-eclipse.jar,' +
					fileTree(dir: 'lib')
					.collect { 'lib/' + it.name }
					.toSorted {a, b -> a.startsWith('lib/convertigo') ? -1 : b.startsWith('lib/convertigo') ? 1 : a <=> b }
					.join(','),
				'Require-Bundle': ['org.eclipse.core.expressions',
					'org.eclipse.core.resources',
					'org.eclipse.core.runtime',
					'org.eclipse.e4.ui.css.swt.theme',
					'org.eclipse.egit.ui',
					'org.eclipse.jface.text',
					'org.eclipse.jgit',
					'org.eclipse.swt',
					'org.eclipse.ui',
					'org.eclipse.ui.console',
					'org.eclipse.ui.editors',
					'org.eclipse.ui.genericeditor',
					'org.eclipse.ui.ide',
					'org.eclipse.ui.workbench.texteditor',
					'org.eclipse.ui.ide',
					'org.eclipse.ui.views',
					'org.eclipse.wst.sse.ui',
					'org.eclipse.wst.sse.core',
					'org.eclipse.wildwebdeveloper'
					/*,
					'org.eclipse.wst.jsdt.ui',
					'org.eclipse.wst.jsdt.core',
					'ts.eclipse.ide.core'*/].join(',')
			)
		}.writeTo(manifest)
		manifest.text = manifest.text.replaceAll("Import-Package: .*\\s+", "")
	}
}

task copyLicense(type: Copy) {
	group 'convertigo'
	
	from '../license.txt'
	into 'src/com/twinsoft/convertigo/eclipse/wizards/setup'
}

project(':engine').afterEvaluate {
	syncLib {
		inputs.property('engine-dependency', '' + project(':engine').configurations.runtimeClasspath.getAllDependencies())
	}
}

task catlClasses(type: Copy) {
	def classesDir = file('catl/dist/classes')
    from('bin/main') {
    	exclude 'com/twinsoft/convertigo/**/*BeanInfo.class'
    	
    	//beans
    	include 'com/twinsoft/convertigo/beans/core/RequestableObject*.class'
    	
    	include 'com/twinsoft/convertigo/beans/connectors/SqlConnector*.class'
    	
    	include 'com/twinsoft/convertigo/beans/rest/AbstractRestOperation*.class'
    	
    	include 'com/twinsoft/convertigo/beans/transactions/SqlTransaction*.class'
    	
    	// eclipse
    	
    	// engine
   		include 'com/twinsoft/convertigo/engine/ConvertigoError*.class'
     	include 'com/twinsoft/convertigo/engine/ProductVersion*.class'
    	
        include 'com/twinsoft/convertigo/engine/providers/sapjco/SapJcoDestinationDataProvider*.class'
        include 'com/twinsoft/convertigo/engine/providers/sapjco/SapJCoProvider*.class'
    }
    into(classesDir)
    
    // uncomment to preserve file lastModified
    /*def copyDetails = []
    eachFile { copyDetails << it }
    doLast {
    	copyDetails.each { FileCopyDetails details ->
        	def target = new File(destinationDir, details.path)
            if (target.exists()) { target.setLastModified(details.lastModified) }
        }
    }*/
}

task catlStudioPatch(type: Zip, dependsOn: ['catlClasses']) {
	archiveFileName = "patchStudio.zip"
	destinationDir = file('catl/dist/patch')
	preserveFileTimestamps = true
    from('catl/dist/classes')
}

task catlServerPatch(type: Zip, dependsOn: ['catlClasses']) {
	archiveFileName = "patchServer.zip"
	destinationDir = file('catl/dist/patch')
	preserveFileTimestamps = true
    from('catl/dist/classes') {
    	exclude 'com/twinsoft/convertigo/eclipse'
    }
}

task generateCatlPatches(dependsOn: ['catlStudioPatch','catlServerPatch']) {
	group 'catl'
	
	doLast {
		def classesDir = file('catl/dist/classes')
		delete classesDir
		
		def d = new Date().format('yyyyMMdd')
		
		def studioZip = file('catl/dist/patch/patchStudio_'+d+'.zip')
		delete studioZip
		file('catl/dist/patch/patchStudio.zip').renameTo(studioZip)
		
		def serverZip = file('catl/dist/patch/patchServer_'+d+'.zip')
		delete serverZip
		file('catl/dist/patch/patchServer.zip').renameTo(serverZip)
		
		println "VERIFIER si com.twinsoft.convertigo.engine.ProductVersion.tag est bien positionné à patch-" + d + " !!"
		println "Si ce n'est pas le cas, modifier le à la date du jour et relancer la tâche !"
	}
}