plugins {
	id 'eclipse'
	id 'java'
	id 'java-gradle-plugin'
	id 'maven-publish'
}

group = 'com.convertigo'
version = version.replace('beta', 'SNAPSHOT')

sourceSets {
	main {
		java {
			srcDirs = ['src']
		}
		resources {
			srcDirs = ['src']
		}
	}
}

compileJava.dependsOn ':engine:publish'

dependencies {
	implementation project(':engine'), {
		exclude group: 'convertigo', module: 'placeholders'
	}
	implementation "com.convertigo:dependencies:$version"
	runtimeOnly    'javax.servlet:servlet-api:2.5'
}

gradlePlugin {
	plugins {
		convertigoPlugin {
			id = 'convertigo'
			implementationClass = 'com.convertigo.gradle.ConvertigoPlugin'
			displayName = 'Convertigo Plugin'
			description = 'Enable tasks to build and deploy Convertigo projects'
		}
	}
}

tasks['publishToMavenLocal'].dependsOn(tasks.getByPath(':engine:publishToMavenLocal'))
tasks['publish'].dependsOn(tasks.getByPath(':engine:publish'))
tasks['eclipse'].doLast {
	mkdir 'build/pluginUnderTestMetadata'
}